#!/usr/bin/env yarn repl -s

-- This script tests a hypothetical upgrade with manual ALT claims and manual ALT speed setting

PrintTransactionLogs
Alias AltHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias DAIWhale "0x47ac0Fb4F2D84898e4D9E7b4DaB3C24507a6D503"
Alias BigBorrower "0x847956f7f7ff49714fb2d70a0d0cd44a6376990f"
Web3Fork "https://mainnet-eth.avastorm.finance/@11458477" (AltHolder USDCWhale DAIWhale)
UseConfigs mainnet

-- Deploy the flywheel impl
ComptrollerImpl Deploy Standard ComptrollerG7

-- Baseline USDC alt speed before the patch
Assert Equal (Comptroller AltSpeed cUSDC) 5.3786477206671197e16

-- Mint tokens
From DAIWhale (Trx GasPrice 0 (Erc20 DAI Approve cDAI UInt256Max))
From DAIWhale (Trx GasPrice 0 (AToken cDAI Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve cUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (AToken cUSDC Mint 10000e6))

-- Baseline ALT claim speed
Assert Equal (Comptroller CheckIsComped cDAI) True

Comptroller ClaimAlt BigBorrower
AdvanceBlocks 1000
Expect Changes (Erc20 Alt TokenBalance BigBorrower) 2.893496802261224189e18
Comptroller ClaimAlt BigBorrower

-- Propose to apply the patch

From AltHolder (Alt Delegate AltHolder)
From AltHolder (Governor GovernorAlpha Propose "Disable automatic alt speed refresh and automatic claims and change alt speed" [(Address Unitroller) (Address ComptrollerG7) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setAltSpeed(address,uint256)"] [[(Address ComptrollerG7)] [(Address Unitroller)] [(Address cUSDC) 30000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From AltHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ComptrollerImpl ComptrollerG7 MergeABI

Comptroller ClaimAlt BigBorrower
AdvanceBlocks 1000
Expect Changes (Erc20 Alt TokenBalance BigBorrower) 2.893496802261224189e18
Comptroller ClaimAlt BigBorrower

Print "Upgrade OK!"
